import raylib
import ai

# -- Components (Plain Data Types) --
type PaddleTransform {
  x: Float
  y: Float
}

type Velocity {
  dx: Float
  dy: Float
}

# -- Systems (Functions acting on Components) --
func movementSystem(pos: mod PaddleTransform, vel: view Velocity) {
  pos.x = pos.x + vel.dx
  pos.y = pos.y + vel.dy
}

func bounceSystem(
  pos: view PaddleTransform,
  vel: mod Velocity,
  radius: Float,
  height: Float,
) {
  if pos.y < radius {
    vel.dy = 4.0
  }
  if pos.y > height - radius {
    vel.dy = -4.0
  }
}

func paddleCollisionSystem(
  bPos: view PaddleTransform,
  bVel: mod Velocity,
  pPos: view PaddleTransform,
  pWidth: Float,
  pHeight: Float,
  isLeft: Int,
) {
  if isLeft is 1 {
    if bPos.x - 10.0 < pPos.x + pWidth {
      if bPos.y > pPos.y {
        if bPos.y < pPos.y + pHeight {
          if bVel.dx < 0.0 {
            # Bounce and speed up
            bVel.dx = -bVel.dx + 1.2
          }
        }
      }
    }
  } else {
    if bPos.x + 10.0 > pPos.x {
      if bPos.y > pPos.y {
        if bPos.y < pPos.y + pHeight {
          if bVel.dx > 0.0 {
            # Bounce and speed up
            bVel.dx = -bVel.dx - 1.2
          }
        }
      }
    }
  }
}

func paddleInputSystem(
  pos: mod PaddleTransform,
  speed: Float,
  height: Float,
  pHeight: Float,
) {
  # W (87) / Up (265)
  if isKeyDown(key: 87) is 1 {
    pos.y = pos.y - speed
  }
  if isKeyDown(key: 265) is 1 {
    pos.y = pos.y - speed
  }
  # S (83) / Down (264)
  if isKeyDown(key: 83) is 1 {
    pos.y = pos.y + speed
  }
  if isKeyDown(key: 264) is 1 {
    pos.y = pos.y + speed
  }
  # Bounds check
  if pos.y < 0.0 {
    pos.y = 0.0
  }
  if pos.y > height - pHeight {
    pos.y = height - pHeight
  }
}

func drawUISystem(p1Score: Int, p2Score: Int, winner: Int) {
  if winner <= 0 {
    drawText(
      text: "{p1Score} - {p2Score}",
      x: 350.0,
      y: 20.0,
      fontSize: 40.0,
      r: 255,
      g: 145,
      b: 0,
      a: 255,
    )
  }

  if winner is 1 {
    drawText(
      text: "YOU WIN",
      x: 350.0,
      y: 200.0,
      fontSize: 40.0,
      r: 255,
      g: 145,
      b: 0,
      a: 255,
    )
  }
  if winner is 2 {
    drawText(
      text: "CPU WINS",
      x: 350.0,
      y: 200.0,
      fontSize: 40.0,
      r: 255,
      g: 145,
      b: 0,
      a: 255,
    )
  }
}

# -- Main Entry --
func main() {
  # Initializing Entities (as local component instances)
  initWindow(width: 800, height: 450, title: "Rae Advanced Pong (ECS)")
  setTargetFPS(fps: 60)
  def p1Pos: PaddleTransform = { x: 20.0, y: 175.0 }
  def p2Pos: PaddleTransform = { x: 760.0, y: 175.0 }
  def bPos: PaddleTransform = { x: 400.0, y: 225.0 }
  def bVel: Velocity = { dx: 4.0, dy: 4.0 }
  def aiState: AIState = { state: 0, timer: 0, targetY: 225.0, decision: 0 }
  def p1Score: Int = 0
  def p2Score: Int = 0

  def scoreToWinMatch: Int = 3
  def winner: Int = 0

  loop windowShouldClose() is 0 {
    # 1. Input Phase
    # 2. Update Phase
    # AI System
    paddleInputSystem(
      pos: p1Pos,
      speed: 5.0,
      height: 450.0,
      pHeight: 100.0,
    )
    p2Pos.y = paddleAiSystem(ai: aiState, ballY: bPos.y, paddleY: p2Pos.y)
    movementSystem(pos: bPos, vel: bVel)
    bounceSystem(
      pos: bPos,
      vel: bVel,
      radius: 10.0,
      height: 450.0,
    )
    paddleCollisionSystem(
      bPos: bPos,
      bVel: bVel,
      pPos: p1Pos,
      pWidth: 20.0,
      pHeight: 100.0,
      isLeft: 1,
    )
    # Reset if out of bounds and increment score
    paddleCollisionSystem(
      bPos: bPos,
      bVel: bVel,
      pPos: p2Pos,
      pWidth: 20.0,
      pHeight: 100.0,
      isLeft: 0,
    )
    if bPos.x < 0.0 {
      p2Score = p2Score + 1
      bPos.x = 400.0
      bPos.y = 225.0
      bVel.dx = 4.0
    }
    if bPos.x > 800.0 {
      p1Score = p1Score + 1
      bPos.x = 400.0
      bPos.y = 225.0
      bVel.dx = -4.0
    }
    # Check if somebody won
    if p1Score is scoreToWinMatch {
      winner = 1
    }
    if p2Score is scoreToWinMatch {
      winner = 2
    }
    # 3. Render Phase
    beginDrawing()
    clearBackground(
      r: 20,
      g: 20,
      b: 20,
      a: 255,
    )
    # Draw paddles
    drawUISystem(p1Score: p1Score, p2Score: p2Score, winner: winner)
    drawRectangle(
      x: 20.0,
      y: p1Pos.y,
      width: 20.0,
      height: 100.0,
      r: 255,
      g: 255,
      b: 255,
      a: 255,
    )
    # Draw ball
    drawRectangle(
      x: 760.0,
      y: p2Pos.y,
      width: 20.0,
      height: 100.0,
      r: 255,
      g: 255,
      b: 255,
      a: 255,
    )
    drawCircle(
      x: bPos.x,
      y: bPos.y,
      radius: 10.0,
      r: 255,
      g: 255,
      b: 255,
      a: 255,
    )
    endDrawing()
  }
  closeWindow()
}
