# Core library for Rae
extern func nextTick(): ret Int
extern func rae_time_ms(): ret Int

extern func sleepMs(ms: Int)

# -- Generic List Implementation (Rae-native) --
type List(T) {
  data: Buffer(T)
  length: Int
  capacity: Int
}

func createList(T)(initialCap: Int): ret List(T) {
  ret {
    data: __buf_alloc(size: initialCap),
    length: 0,
    capacity: initialCap,
  }
}

func listGrow(T)(this: mod List(T)) {
  def newCap: Int = this.capacity * 2
  if newCap is 0 { newCap = 4 }
  
  def newData: Buffer(T) = __buf_alloc(size: newCap)
  __buf_copy(
    src: this.data, src_off: 0,
    dst: newData, dst_off: 0,
    len: this.length
  )
  __buf_free(buf: this.data)
  this.data = newData
  this.capacity = newCap
}

func listAdd(T)(this: mod List(T), value: T) {
  if this.length is this.capacity {
    listGrow(this)
  }
  
  __buf_set(buf: this.data, index: this.length, value: value)
  this.length = this.length + 1
}

func listGet(T)(this: view List(T), index: Int): ret T {
  if index < 0 or index >= this.length {
    ret none
  }
  ret __buf_get(buf: this.data, index: index)
}

func listSet(T)(this: mod List(T), index: Int, value: T) {
  if index < 0 or index >= this.length {
    ret
  }
  __buf_set(buf: this.data, index: index, value: value)
}

func listInsert(T)(this: mod List(T), index: Int, value: T) {
  if index < 0 or index > this.length {
    ret
  }
  
  if this.length is this.capacity {
    listGrow(this)
  }
  
  # Shift elements to the right
  if index < this.length {
    __buf_copy(
      src: this.data, src_off: index,
      dst: this.data, dst_off: index + 1,
      len: this.length - index
    )
  }
  
  __buf_set(buf: this.data, index: index, value: value)
  this.length = this.length + 1
}

func listPop(T)(this: mod List(T)): ret T {
  if this.length is 0 {
    ret none
  }
  def val: T = __buf_get(buf: this.data, index: this.length - 1)
  this.length = this.length - 1
  ret val
}

func listRemove(T)(this: mod List(T), index: Int) {
  if index < 0 or index >= this.length {
    ret
  }
  
  # Shift elements to the left
  if index < this.length - 1 {
    __buf_copy(
      src: this.data, src_off: index + 1,
      dst: this.data, dst_off: index,
      len: this.length - index - 1
    )
  }
  
  this.length = this.length - 1
}

func listClear(T)(this: mod List(T)) {
  this.length = 0
}

func listLength(T)(this: view List(T)): ret Int {
  ret this.length
}

func listFree(T)(this: mod List(T)) {
  __buf_free(buf: this.data)
  this.length = 0
  this.capacity = 0
}

