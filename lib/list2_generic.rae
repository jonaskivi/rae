# Generic List2 implementation for Rae
type List2Generic(T) {
  data: Buffer(T)
  length: Int
  capacity: Int
}

func createList2Generic(T)(initialCap: Int) ret List2Generic(T) {
  ret List2Generic(T) {
    data: __buf_alloc(size: initialCap)
    length: 0
    capacity: initialCap
  }
}

func grow(T)(this: mod List2Generic(T)) {
  let newCap: Int = this.capacity * 2
  if newCap is 0 {
    newCap = 4
  }
  let newData: Buffer(T) = __buf_alloc(size: newCap)
  __buf_copy(
    src: this.data
    src_off: 0
    dst: newData
    dst_off: 0
    len: this.length
  )
  __buf_free(buf: this.data)
  this.data = newData
  this.capacity = newCap
}

func add(T)(this: mod List2Generic(T), value: T) {
  if this.length is this.capacity {
    grow(this)
  }
  __buf_set(buf: this.data, index: this.length, value: value)
  this.length = this.length + 1
}

func get(T)(this: view List2Generic(T), index: Int) ret T {
  ret __buf_get(buf: this.data, index: index)
}

func length(T)(this: view List2Generic(T)) ret Int {
  ret this.length
}
