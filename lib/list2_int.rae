# -- List2 Prototype (Int only) --
type List2Int {
  data: Buffer Int
  length: Int
  capacity: Int
}

func createList2Int(initialCap: Int): ret List2Int {
  ret {
    data: __buf_alloc(size: initialCap),
    length: 0,
    capacity: initialCap,
  }
}

func grow(this: mod List2Int) {
  def newCap: Int = this.capacity * 2
  if newCap is 0 { newCap = 4 }
  
  def newData: Buffer Int = __buf_alloc(size: newCap)
  __buf_copy(
    src: this.data, src_off: 0,
    dst: newData, dst_off: 0,
    len: this.length
  )
  __buf_free(buf: this.data)
  this.data = newData
  this.capacity = newCap
}

func add(this: mod List2Int, value: Int) {
  if this.length is this.capacity {
    grow(this)
  }
  
  __buf_set(buf: this.data, index: this.length, value: value)
  this.length = this.length + 1
}

func get(this: view List2Int, index: Int): ret Int {
  if index < 0 or index >= this.length {
    ret 0
  }
  ret __buf_get(buf: this.data, index: index)
}

func set(this: mod List2Int, index: Int, value: Int) {
  if index < 0 or index >= this.length {
    ret
  }
  __buf_set(buf: this.data, index: index, value: value)
}

func insert(this: mod List2Int, index: Int, value: Int) {
  if index < 0 or index > this.length {
    ret
  }
  
  if this.length is this.capacity {
    grow(this)
  }
  
  # Shift elements to the right
  if index < this.length {
    __buf_copy(
      src: this.data, src_off: index,
      dst: this.data, dst_off: index + 1,
      len: this.length - index
    )
  }
  
  __buf_set(buf: this.data, index: index, value: value)
  this.length = this.length + 1
}

func pop(this: mod List2Int): ret Int {
  if this.length is 0 {
    ret 0
  }
  def val: Int = __buf_get(buf: this.data, index: this.length - 1)
  this.length = this.length - 1
  ret val
}

func remove(this: mod List2Int, index: Int) {
  if index < 0 or index >= this.length {
    ret
  }
  
  # Shift elements to the left
  if index < this.length - 1 {
    __buf_copy(
      src: this.data, src_off: index + 1,
      dst: this.data, dst_off: index,
      len: this.length - index - 1
    )
  }
  
  this.length = this.length - 1
}

func clear(this: mod List2Int) {
  this.length = 0
}

func contains(this: view List2Int, value: Int): ret Bool {
  def i: Int = 0
  loop i < this.length {
    if __buf_get(buf: this.data, index: i) is value {
      ret true
    }
    i = i + 1
  }
  ret false
}

func list2Free(this: mod List2Int) {
  __buf_free(buf: this.data)
  this.length = 0
  this.capacity = 0
}
